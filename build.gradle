plugins {
    id 'java-gradle-plugin'
	id 'maven-publish'
}

sourceCompatibility = targetCompatibility = 1.8

group = 'com.minecrafttas'
version = '2.0.1'
archivesBaseName = 'mcp4gradle'

repositories {
	mavenCentral()
	gradlePluginPortal()
	maven { url "https://maven.mgnet.work/main" }
}

dependencies {
	// Add zip4j for unzipping easily
	implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '2.11.5'
	
	// Add apache commons csv for reading mappings
	implementation group: 'org.apache.commons', name: 'commons-csv', version: '1.10.0'
	
	// Add diff utils for patch file generation
	implementation group: 'io.github.java-diff-utils', name: 'java-diff-utils', version: '4.12'
	
	// Add required decompilation dependencies
	implementation group: 'com.minecrafttas', name: 'mcinjector-og', version: '2.0.0'
	implementation group: 'com.minecrafttas', name: 'retroguard-og', version: '3.0.0'
	implementation group: 'com.minecrafttas', name: 'jadretro-og', version: '1.6.0'
	
	// Add shadow plugin for exporting
	implementation group: 'com.github.johnrengelman', name: 'shadow', version: '8.1.1'
}

gradlePlugin {
    plugins {
        mcp4gradle {
            id = project.archivesBaseName
            implementationClass = project.group + '.' + project.archivesBaseName + '.' + 'MCP4Gradle'
        }
    }
}

publishing {
	repositories {
		maven {
			name = "mgnetwork"
			url = "https://maven.mgnet.work/main"
			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}
